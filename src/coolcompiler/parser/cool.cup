/*
 *  cool.cup
 *              Parser definition for the COOL language.
 *
 * @author Adam Brauns, Andrew Bronas, Brian Maresso, Connor Grady
 */

import java_cup.runtime.*;

/* Stuff enclosed in {: :} is copied verbatim to the class containing
all parser actions.  All the extra variables/functions you want to use
in the semantic actions should go here.  Don't remove or modify anything
that was there initially.  */

action code {:

    int curr_lineno() {
    return ((CoolTokenLexer)parser.getScanner()).curr_lineno();
    }

    AbstractSymbol curr_filename() {
    return ((CoolTokenLexer)parser.getScanner()).curr_filename();
    }
:}

/************************************************************************/
/*                DONT CHANGE ANYTHING IN THIS SECTION                  */

parser code {:
    int omerrs = 0;

    public void syntax_error(Symbol cur_token) {
        int lineno = action_obj.curr_lineno();
    String filename = action_obj.curr_filename().getString();
        System.err.print("\"" + filename + "\", line " + lineno +
                           ": parse error at or near ");
        Utilities.printToken(cur_token);
    omerrs++;
    if (omerrs>50) {
       System.err.println("More than 50 errors");
          System.exit(1);
          }
    }

    public void unrecovered_syntax_error(Symbol cur_token) {
    }
:}

/* Declare the terminals; a few have types for associated lexemes.  The
token ERROR is never used in the parser; thus, it is a parse error when
the lexer returns it.  */

terminal CLASS, ELSE, FI, IF, IN, INHERITS, LET, LET_STMT, LOOP, POOL, THEN, WHILE;
terminal CASE, ESAC, OF, DARROW, NEW, ISVOID;
terminal ASSIGN, NOT, LE, ERROR;
terminal PLUS, DIV, MINUS, MULT, EQ, LT, DOT, NEG, COMMA, SEMI, COLON;
terminal LPAREN, RPAREN, AT, LBRACE, RBRACE;
terminal AbstractSymbol STR_CONST, INT_CONST;
terminal Boolean BOOL_CONST;
terminal AbstractSymbol TYPEID, OBJECTID;

/*  DON'T CHANGE ANYTHING ABOVE THIS LINE, OR YOUR PARSER WONT WORK       */
/**************************************************************************/

   /* Complete the nonterminal list below, giving a type for the semantic
      value of each non terminal. (See the CUP documentation for details. */

nonterminal attr attribute; /* Attributes */
nonterminal Cases case_list; /* Cases */
nonterminal Classes class_list; /* Class coll */
nonterminal class_c class; /* Class */
nonterminal Expression expr; /* Expression */
nonterminal Expressions dispatch_arg_list; /* Args */
nonterminal Expressions expression_list; /* Expression coll */
nonterminal Features feature_list; /* Feature coll */
nonterminal Formal formal; /* Formal */
nonterminal Formals formal_list; /* Formal coll */
nonterminal let let_; /* let */
nonterminal method method_; /* Method */
nonterminal programc program; /* Program */

/* Precedence declarations go here. */
precedence left ASSIGN;
precedence nonassoc LE, LT, EQ;

precedence left PLUS, MINUS;
precedence left MULT, DIV;
precedence left ISVOID;
precedence left NEG;
precedence left AT;
precedence left DOT;


program
       ::= class_list:cl
           {: RESULT = new programc(curr_lineno(), cl); :}
        ;

class_list
    /* single class */
    ::= class:c
        {: RESULT = (new Classes(curr_lineno())).appendElement(c); :}
        /* several classes */
        | class_list:cl class:c
            {: RESULT = cl.appendElement(c); :}
        ;


/* If no parent is specified, the class inherits from the Object class */
class
    ::= CLASS TYPEID:n LBRACE feature_list:f RBRACE SEMI
        {: RESULT = new class_c(curr_lineno(), n,
                              AbstractTable.idtable.addString("Object"),
                                               f, curr_filename()); :}
                                               | CLASS TYPEID:n INHERITS TYPEID:p LBRACE feature_list:f RBRACE SEMI
                                                   {: RESULT = new class_c(curr_lineno(), n, p, f, curr_filename()); :}
    | error SEMI
    ;

/* Feature list may be empty, but no empty features in list. */
feature_list
    ::= /* empty */
        {: RESULT = new Features(curr_lineno()); :}
        | attribute:a
            {: RESULT = new Features(curr_lineno()).appendElement(a); :}
        | method_:m
            {: RESULT = new Features(curr_lineno()).appendElement(m); :}
            | feature_list:fs attribute:a
                {: RESULT = fs.appendElement(a);:}
            | feature_list:fs method_:m
                {: RESULT = fs.appendElement(m);:}
                ;

method_
    ::=  OBJECTID:o LPAREN formal_list:fs RPAREN COLON TYPEID:t LBRACE expr:e RBRACE SEMI
        {: RESULT = new method(curr_lineno(), o, fs, t, e); :}
    |    error SEMI
    ;

formal_list
    ::= /* empty */
        {: RESULT = new Formals(curr_lineno()); :}
    |   formal:f
        {: RESULT = new Formals(curr_lineno()).appendElement(f); :}
    |   formal_list:fs COMMA formal:f
        {: RESULT = fs.appendElement(f); :}
    ;
formal
    ::= OBJECTID:o COLON TYPEID:t
        {: RESULT = new formalc(curr_lineno(), o, t);:}
    ;

attribute
    ::= OBJECTID:o COLON TYPEID:t SEMI
        {: RESULT = new attr(curr_lineno(),o,t, new no_expr(curr_lineno())); :}
    |   OBJECTID:o COLON TYPEID:t ASSIGN expr:e SEMI
        {: RESULT = new attr(curr_lineno(),o,t, e ); :}
    |   error SEMI
    ;

expr
    ::= OBJECTID:o ASSIGN expr:e
        {: RESULT = new assign(curr_lineno(), o, e); :}
    |   OBJECTID:o ASSIGN error

    |   expr:e DOT OBJECTID:m LPAREN dispatch_arg_list:as RPAREN
        {: RESULT = new dispatch(curr_lineno(), e , m, as); :}

    |   OBJECTID:m LPAREN dispatch_arg_list:as RPAREN
        {: RESULT = new dispatch(curr_lineno(), new object(curr_lineno(), AbstractTable.idtable.addString("self")) , m, as); :}

    |   expr:e AT TYPEID:t DOT OBJECTID:m LPAREN dispatch_arg_list:as RPAREN
        {: RESULT = new static_dispatch(curr_lineno(), e,t, m, as); :}

    |   IF expr:p THEN expr:t ELSE expr:e FI
        {: RESULT = new cond(curr_lineno(), p, t, e); :}

    |   WHILE expr:pred LOOP expr:body POOL
        {: RESULT = new loop(curr_lineno(), pred, body); :}

    |   CASE expr:e OF  case_list:cs ESAC
        {: RESULT = new typcase(curr_lineno(), e, cs); :}

    |   LPAREN expr:e RPAREN
        {: RESULT = e; :}

    |   LBRACE expression_list:es RBRACE
        {: RESULT = new block(curr_lineno(), es); :}


    |   LET let_:l
        {: RESULT = l; :}

    |   expr:e1 PLUS expr:e2
        {: RESULT = new plus(curr_lineno(), e1, e2); :}

    |   expr:e1 MINUS expr:e2
        {: RESULT = new sub(curr_lineno(), e1, e2); :}

    |   expr:e1 MULT expr:e2
        {: RESULT = new mul(curr_lineno(), e1, e2); :}

    |   expr:e1 DIV expr:e2
        {: RESULT = new divide(curr_lineno(), e1, e2); :}

    |   NEG expr:e
        {: RESULT = new neg(curr_lineno(), e); :}

    |   expr:e1 EQ expr:e2
        {: RESULT = new eq(curr_lineno(), e1, e2); :}

    |   expr:e1 LT expr:e2
        {: RESULT = new lt(curr_lineno(), e1, e2); :}

    |   expr:e1 LE expr:e2
        {: RESULT = new leq(curr_lineno(), e1, e2); :}

    |   NOT expr:e
        {: RESULT = new comp(curr_lineno(), e); :}

    |   STR_CONST:s
        {: RESULT = new string_const(curr_lineno(), AbstractTable.stringtable.addString(s.getString()));:}

    |   INT_CONST:i
        {: RESULT = new int_const(curr_lineno(), AbstractTable.inttable.addString(i.getString()));:}

    |   BOOL_CONST:b
        {: RESULT = new bool_const(curr_lineno(), b); :}

    |    NEW TYPEID:t
        {: RESULT = new new_(curr_lineno(), t); :}

    |    ISVOID expr:e
        {: RESULT = new isvoid(curr_lineno(), e); :}



    |    OBJECTID:o
        {: RESULT = new object(curr_lineno(), o); :}


    ;

let_
    ::= OBJECTID:o COLON TYPEID:t ASSIGN expr:e IN expr:body
        {: RESULT = new let(curr_lineno(), o,t,e,body); :}
    |   OBJECTID:o COLON TYPEID:t IN expr:body
        {: RESULT = new let(curr_lineno(), o,t,new no_expr(curr_lineno()),body); :}
    |   OBJECTID:o COLON TYPEID:t COMMA let_:l
        {: RESULT = new let(curr_lineno(), o,t,new no_expr(curr_lineno()), l); :}
    |   OBJECTID:o COLON TYPEID:t ASSIGN expr:e COMMA let_:l
        {: RESULT = new let(curr_lineno(), o,t,e, l); :}
    |   error COMMA let_
    ;

expression_list
    ::= expr:e SEMI
        {: RESULT = new Expressions(curr_lineno()).appendElement(e); :}
    | expression_list:es expr:e SEMI
        {: RESULT = es.appendElement(e); :}
    | error SEMI
        {: RESULT = new Expressions(curr_lineno()); :}
    ;

dispatch_arg_list
    ::= /**/
        {: RESULT = new Expressions(curr_lineno()); :}
    |    expr:e
        {: RESULT  = new Expressions(curr_lineno()).appendElement(e); :}
    |   dispatch_arg_list:as COMMA expr:e
        {: RESULT = as.appendElement(e); :}
    ;


case_list
    ::=   OBJECTID:o COLON TYPEID:t DARROW expr:e SEMI
          {: RESULT = new Cases(curr_lineno()).appendElement(new branch(curr_lineno(), o,t,e)); :}
    |     case_list:cs OBJECTID:o COLON TYPEID:t DARROW expr:e SEMI
          {: RESULT = cs.appendElement(new branch(curr_lineno(),o,t,e)); :}
     ;

